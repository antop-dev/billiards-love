plugins {
    id "org.springframework.boot" version "2.5.4"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id "java"
    id "org.sonarqube" version "3.0"
    id "jacoco"
    id "com.epages.restdocs-api-spec" version "0.11.5"
    id "org.asciidoctor.convert" version "1.5.8"
}

group = "org.antop"
version = "1.0.0"

project.sourceCompatibility = 1.8
project.targetCompatibility = 1.8

ext {
    snippetsDir = file("build/generated-snippets")
    // versions
    sentryVersion = "5.1.2"
    mariadbVersion = "2.7.4"
    commonsIoVersion = "2.11.0"
    commonsLang3Version = "3.12.0"
    commonsCodecVersion = "1.15"
    jasyptVersion = "2.1.2"
    log4jdbcSpring = "2.0.0"
    mapstructVersion = "1.4.2.Final"
    querydslVersion = "4.4.0"
    lombokVersion = "1.8.20"
    lombokMapstructBindingVersion = "0.2.0"
    springRestDocsVersion = "2.0.5.RELEASE"
    swaggerUiVersion = "3.0.0"
    restdocsApiSpec = "0.12.0"
    fakerVersion = "1.0.2"
    jjwtVersion = "0.9.1"
    hamcrestOptionalVersion = "2.0.0"
}

/**
 * Gradle을 통해 빌드 테스트시에 H2 데이터베이스의 인코딩이 안맞춰져서 한글이 전부 깨지게 된다.
 * 그래서 Gradle test 실행시 file.encoding 을 설정한다.
 * https://gist.github.com/rponte/d660919434d094bbd35a1aabf7ef1bf0
 */
compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

// 1st approach
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// 2nd approach
tasks.withType(Test) {
    systemProperty "file.encoding", "UTF-8"
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    // https://github.com/jwtk/jjwt
    implementation "io.jsonwebtoken:jjwt:$jjwtVersion"
    // https://github.com/querydsl/querydsl/tree/master/querydsl-jpa
    implementation "com.querydsl:querydsl-jpa:$querydslVersion"
    // https://projectlombok.org/
    compileOnly "org.projectlombok:lombok:$lombokVersion"
    developmentOnly "org.springframework.boot:spring-boot-devtools"
    // https://mapstruct.org/
    compileOnly "org.mapstruct:mapstruct:$mapstructVersion"
    // https://docs.sentry.io/platforms/java/guides/spring-boot/
    implementation "io.sentry:sentry-spring-boot-starter:$sentryVersion"
    // https://github.com/candrews/log4jdbc-spring-boot-starter/
    implementation "com.integralblue:log4jdbc-spring-boot-starter:$log4jdbcSpring"
    // https://mariadb.com/kb/en/about-mariadb-connector-j/
    implementation "org.mariadb.jdbc:mariadb-java-client:$mariadbVersion"
    // https://commons.apache.org/proper/commons-io/
    implementation "commons-io:commons-io:$commonsIoVersion"
    // https://commons.apache.org/proper/commons-lang/
    implementation "org.apache.commons:commons-lang3:$commonsLang3Version"
    // https://commons.apache.org/proper/commons-codec/
    implementation "commons-codec:commons-codec:$commonsCodecVersion"
    // https://github.com/ulisesbocchio/jasypt-spring-boot
    implementation "com.github.ulisesbocchio:jasypt-spring-boot-starter:$jasyptVersion"
    /**
     * https://wise-develop.tistory.com/18
     * 순서에 주의해야 한다!!!!!!!
     * 맵스트럭이 롬복보다 위에 있어야 한다.
     */
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
    annotationProcessor "org.mapstruct:mapstruct-processor:$mapstructVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok-mapstruct-binding:$lombokMapstructBindingVersion"
    annotationProcessor "com.querydsl:querydsl-apt:$querydslVersion:jpa"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    /**
     * test
     */
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework.security:spring-security-test"
    // https://github.com/DiUS/java-faker
    testImplementation "com.github.javafaker:javafaker:$fakerVersion"
    // https://github.com/npathai/hamcrest-optional
    testImplementation "com.github.npathai:hamcrest-optional:$hamcrestOptionalVersion"
    // https://spring.io/projects/spring-restdocs
    testImplementation "org.springframework.restdocs:spring-restdocs-mockmvc:$springRestDocsVersion"
    // swagger-ui
    implementation "io.springfox:springfox-swagger-common:$swaggerUiVersion"
    implementation "io.springfox:springfox-swagger-ui:$swaggerUiVersion"
    // https://github.com/ePages-de/restdocs-api-spec
    testImplementation "com.epages:restdocs-api-spec-mockmvc:$restdocsApiSpec"
}


test {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

jacoco {
    toolVersion "0.8.5"
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

sonarqube {
    properties {
        property "sonar.organization", "antop-dev-github"
        property "sonar.projectKey", "antop-dev_billiards-love:back-end"
        property "sonar.projectName", "billiards-love:back-end"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.java.source", "1.8"
        property "sonar.java.target", "1.8"
    }
}

asciidoctor {
    dependsOn test
    inputs.dir snippetsDir
    finalizedBy "copyDocument"
}

asciidoctor.doFirst {
    delete file("src/main/resources/static/index.html")
}

task copyDocument(type: Copy) {
    dependsOn asciidoctor
    from file("build/asciidoc/html5")
    into file("src/main/resources/static")
}

openapi3 {
    servers = [{ url = "http://localhost:9000" }]
    title = "당구사랑"
    version = "1.0.0"
    description = "당구사랑 API"
    format = "json"
    separatePublicApi = false
    outputDirectory = "src/main/resources/api-docs/"
}
